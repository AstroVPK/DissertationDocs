import os.path

from site_cons.site_init import link_wtk_graph, link_pyfit


FIGURES = ['v-dep', 'loading-rate', 'i-dep']

# Get the passed in environment.
Import('env')

v_dep_data_dir = env.Dir('v-dep.d')
v_dep_data_files = [
    f for f in env.Glob(os.path.join(str(v_dep_data_dir), '*'))
    if 'fit' not in str(f)]

pyfit = link_pyfit(env)

v_dep_fit_files = []
for f in v_dep_data_files:
    fit = env.Command(
        str(f)+'.fit',
        [f, pyfit],
        "python %s -m math:log -f 'A*log(x,10)+B' -v $SOURCE > $TARGET"
        % pyfit[0].get_abspath())
    fit_dat = env.Command(
        str(fit[0])+'.dat',
        fit,
        "sed -n 's/^[A-Z]: //p' $SOURCE > $TARGET")
    v_dep_fit_files.append(fit_dat)

v_dep_data = v_dep_data_files + v_dep_fit_files

wtk_graph = link_wtk_graph(env)

for fig in FIGURES:
    asyfile = '%s.asy' % fig  # static .asy file
    pyfig = fig.replace('-', '_')
    data = '%s_data' % (pyfig)
    asydata = []
    if data in globals():     # generated data dependencies
        asydata = globals()[data]
    env.Asymptote([asyfile, wtk_graph] + asydata)

# Pass back the modified environment.
Return('env')
